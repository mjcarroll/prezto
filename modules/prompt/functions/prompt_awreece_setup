#
# A simple theme that displays relevant, contextual information.
#
# Authors:
#   Alex Reece <awreece@gmail.com>
#
# Screenshots:
#   http://codearcana.com/posts/2013/11/06/my-zsh-theme.html
#   http://codearcana.com/images/zsh_theme.png
#

function prompt_awreece_help {
cat <<EOH
Prompt will look like:
#############################################################################
+----------- Last part in path to current working directory.
|
|        +-- A '#' if root shell, colored green if the last command was
|        |   successful and red otherwise.
|        |
|        |   Duration of last command, colored green if last command --+
|        |   was successful and red otherwise.                         |
|        |                                                             |
|        |   ssh user and hostname -------------------------+          |
|        |   (if connected via ssh).                        |          |
|        |                                                  |          |
|        |   Full path to current working --+               |          |
|        |   directory (if longer than      |               |          |
|        |   than 1 segment).               |               |          |
|        |                                  |               |          |
|        |   Number of background jobs --+  |               |          |
|        |   (if any).                   |  |               |          |
v        v                               v  v               v          v
#############################################################################
Developer%                               1& ~/bin/Developer [alex@cmu] 2.001s
EOH
}

function prompt_awreece_pre {
  vcs_info
}

function prompt_awreece_setup {
  prompt_opts=(cr percent subst)

  # Variables used for prompts.
  local background_job_status='%(1j.%F{yellow}%j&%f .)'
  local full_directory='%(2~.%F{blue}%~%f .)'
  local ssh_host=''
  if prompt_awreece_is_ssh; then
    ssh_host='%F{cyan}[%n@%m]%f '
  fi

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_awreece_pre

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable bzr git hg svn
  zstyle ':vcs_info:*' check-for-changes true
  zstyle ':vcs_info:*' stagedstr '%F{green}●%f'
  zstyle ':vcs_info:*' unstagedstr '%F{yellow}●%f'
  zstyle ':vcs_info:*' formats '%F{cyan}(%b%f%u%c)'
  zstyle ':vcs_info:*' actionformats "%F{cyan}(%b%f%u%c)%F{green}(%s%f)"
  zstyle ':vcs_info:(sv[nk]|bzr):*' branchformat '%b|%F{cyan}%r%f'
  zstyle ':vcs_info:git*+set-message:*' hooks git-status

  # Define prompts.
  PROMPT='%F{blue}%1~${vcs_info_msg_0_}%f%F{magenta}%#%f '
  RPROMPT="${background_job_status}${full_directory}${ssh_host}"
}

# Return a zero exit status (true) iff the current shell is controlled via ssh.
function prompt_awreece_is_ssh {
  # Actually, sudo etc clear a bunch of environment variables, including
  # SSH_CONNECTION, so this doesn't always work. Unfortunately, I don't know
  # the best way to make it work. For now, I'll hide it in a function and I can
  # update it when I figure out a better way later.
  [[ -n $SSH_CONNECTION ]]
}

function prompt_awreece_preview {
  local +h PROMPT='%# '
  local +h RPROMPT=''
  # Set a last_command_time to make it interesting.
  local +h last_commmand_time=3.14159

  prompt_preview_theme 'awreece' "$@"
}

prompt_awreece_setup "$@"
